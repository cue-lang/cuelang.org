---
title: Generating Go types from CUE definitions
tags: [encodings]
authors: [jpluscplusm]
toc_hide: true
---

CUE v0.12.0 introduced an experimental new command:
[`gengotypes`]({{<relref"docs/reference/command/cue-help-exp-gengotypes">}}).
This command generates Go types from CUE definitions and places them in `.go`
files. It uses sensible defaults for the generated Go code that you can
override by configuring various CUE attributes.

This guide shows you how to use the `cue exp gengotypes` command.

## Why was `gengotypes` introduced?

When a Go program works with CUE it can be useful to declare Go types which
mirror its CUE schemas.

For example, if the program loads a YAML configuration file and validates it
using a CUE schema it's often useful to hold the config as a Go struct value
that controls how the program should behave.

Using `gengotypes` allows you to declare your schemas in CUE and automatically
derive compatible Go types -- without having to repeat yourself or maintain two
versions of the same structure.

## Generating Go types

{{< step stepNumber="1" >}}
Start with some CUE that contains at least one definition:
{{< code-tabs >}}
{{< code-tab name="pets.cue" language="cue" area="top-left" >}}
package pet

// These are the only pet types we can handle.
petTypes: ["dog", "cat", "goldfish"]

// A dog tracks information we hold about each dog.
#dog: {
	// A dog has at least one name.
	name!: string
	age?:  float & <30
}
{{< /code-tab >}}{{< /code-tabs >}}

*Hidden* definitions (fields with names that start `_#`) are ignored by the
`gengotypes` command.
{{< /step >}}

{{< step stepNumber="2" >}}
Run the `cue exp gengotypes` command targetting the single CUE package in the
current directory:

```text { title="TERMINAL" type="terminal" codeToCopy="Y3VlIGV4cCBnZW5nb3R5cGVzIC4=" }
$ cue exp gengotypes .
```
The command is silent when it succeeds.
{{< /step >}}

{{< step stepNumber="3" >}}
Observe the contents of the `cue_types_gen.go` file:
{{< code-tabs >}}
{{< code-tab name="cue_types_gen.go" language="go" area="top-left" >}}
// Code generated by "cue exp gengotypes"; DO NOT EDIT.

package pet

// A dog tracks information we hold about each dog.
type Dog struct {
	// A dog has at least one name.
	Name string `json:"name"`

	Age float64 `json:"age,omitempty"`
}
{{< /code-tab >}}{{< /code-tabs >}}
Notice these differences between the contents of
[`pets.cue`](#step-1)
and
[`cue_types_gen.go`](#step-3):

- By default, `gengotypes` ensures the first character of each struct's name is
  upper-cased (`#dog` vs `Dog`).
- The top-level regular field `petTypes` and its comment aren't present in the
  Go file. Only fields inside a definition are translated to Go.
- The optional field `age` has a numeric constraint in CUE (`<30`) which can't
  be expressed in the simpler, less powerful Go type system -- so the Go
  encodes a `float64` type. Generated Go types are guaranteed to accept any
  value accepted by the CUE definitions but, as with `age`, they may be more
  general.
{{< /step >}}

## Controlling the generated Go

If you need to control the generated Go package, type, and field names, you can
use the `@go()` attribute to specify them in your CUE:

{{< step stepNumber="4" >}}
Add another CUE file, `morePets.cue`, containing the following code:
{{< code-tabs >}}
{{< code-tab name="morePets.cue" language="cue" area="top-left" >}}
package pet

@go(acmecorppets)

// A cat represents an instance of a domestic cat.
#cat: {
	@go(cat)

	// Cats consider themselves to have a title, not a name.
	title!:              string @go(Name) // Maintain parity with Dog when emitting Go.
	socialMediaAccount!: string @go(-)    // Don't emit this field as Go.
}

#horse: {
	@go(-)
	status: "Unsupported"
}
{{< /code-tab >}}{{< /code-tabs >}}
{{< /step >}}

{{< step stepNumber="5" >}}
Run the `cue exp gengotypes` command again:

```text { title="TERMINAL" type="terminal" codeToCopy="Y3VlIGV4cCBnZW5nb3R5cGVzIC4=" }
$ cue exp gengotypes .
```
{{< /step >}}

{{< step stepNumber="6" >}}
Observe the new contents of the `cue_types_gen.go` file:
{{< code-tabs >}}
{{< code-tab name="cue_types_gen.go" language="go" area="top-left" >}}
// Code generated by "cue exp gengotypes"; DO NOT EDIT.

package acmecorppets

// A cat represents an instance of a domestic cat.
type cat struct {
	// Cats consider themselves to have a title, not a name.
	Name string `json:"title"`
}

// A dog tracks information we hold about each dog.
type Dog struct {
	// A dog has at least one name.
	Name string `json:"name"`

	Age float64 `json:"age,omitempty"`
}
{{< /code-tab >}}{{< /code-tabs >}}
Notice these differences between the contents of
[`morePets.cue`](#step-4)
and the new
[`cue_types_gen.go`](#step-6):

- At the package level:
  - The `@go(acmecorppets)` attribute renamed the generated Go package from
    `pet`.
  - Because the `cue` command targetted the whole of the `pet` CUE package, the
    dog-related contents of `pets.cue` are included under the renamed Go
    package. Only one package-level attribute is needed.
- Inside `#cat`:
  - The definition-level `@go(cat)` attribute renamed the generated
    Go type from the default that would otherwise have been emitted (`Cat`).
  - The field-level `@go(Name)` attribute renamed the generated Go field from
    `Title`.
  - The field-level `@go(-)` attribute caused the `socialMediaAccount` field to
    be omitted from the generated Go type.
- Inside `#horse`, the definition-level `@go(-)` attribute caused the entire
  definition to be omitted from the generated Go.

You can also use the `@go()` attribute to control the *type* of a generated
field -- see
[`cue help exp gengotypes`]({{<relref"docs/reference/command/cue-help-exp-gengotypes">}})
for more information.
{{< /step >}}

{{<warning>}}
The `gengotypes` command is still in an experimental stage, which means that it
may be changed or removed at any time. The objective is for the CUE project to
gain experience through [your feedback]({{<relref"/community">}}) about this
experimental command, and then move the feature elsewhere.
{{</warning>}}

## Related content

- {{<linkto/related/reference"command/cue-help-exp-gengotypes">}} --
  the built-in help text for the `cue exp gengotypes` command
