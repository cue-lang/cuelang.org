---
title: How to import YAML GitHub Actions workflow files into CUE
weight:
tags:
    - cue command
---

This guide shows you the one-time process of importing pre-existing YAML GitHub
Actions workflow files into CUE. It also shows the related process of exporting
the configuration back into their YAML form that GitHub Actions requires, which
you will perform each time a change is made to a CUE-based workflow file.

## Prerequisites

- You have [CUE installed](https://cuelang.org/docs/install/) locally. This
  allows you to run `cue` commands
- You have a set of YAML GitHub Actions workflow files
  - The examples shown in this guide are taken from CUE's
    [github-actions-example repository](https://github.com/cue-examples/github-actions-example/tree/main/.github/workflows),
    but your use of that repository is not required
- You have a [git client installed](https://git-scm.com/downloads)

## Steps

{{step}}
Change directory into the root of the repository that contains your GitHub
Actions workflow files.\
Ensure you start this process with a clean git state, with no modified files.
For example:

```console
$ cd github-actions-example
$ git status
On branch main
nothing to commit, working tree clean
```

{{step}}
Initialise a CUE module named after the organisation and repository you're
working with:

```console
$ cue mod init github.com/cue-examples/github-actions-example
```

{{step}}
Create a directory for your CUE-based GitHub Actions workflow files. For
example:

```console
$ mkdir -p internal/ci/github
```

{{step}}
Import the YAML-based workflow files into CUE, placing each workflow inside the
top-level `workflow` struct, inside a struct named after its original file:

```console
$ cue import ./.github/workflows/ --with-context -p github -l 'workflow:' -l 'strings.TrimSuffix(path.Base(filename),".yml")' -f
```

{{step}}
Move the newly-created CUE files into their dedicated directory:

```console
$ mv ./.github/workflows/*.cue internal/ci/github
```

{{step}}
Create a CUE "tool" file named `github_actions_tool.cue` in the directory
*above* the workflows' dedicated directory. For example, if you have kept the
same directory layout as the example, this file must be created as
`internal/ci/github_actions_tool.cue`. This tool will perform an on-demand
export of each CUE-based workflow back into its required YAML file.

The file contains a few elements that you must adapt to your repository and
setup after creating the file. These changes are listed after the file's
contents:

```text {title="internal/ci/github_actions_tool.cue", linenos=table}
package ci

import (
	"path"
	"encoding/yaml"
	"tool/file"

	"github.com/cue-examples/github-actions-example/internal/ci/github"
)

_goos: string @tag(os,var=os)

// Regenerate all workflow files 
command: regenerate: {
	workflows: {
		_dir:       path.FromSlash("../../.github/workflows", path.Unix)
		_this_file: "internal/ci/github_actions_tool.cue"
		remove: {
			glob: file.Glob & {
				glob: path.Join([_dir, "*.yml"], _goos)
				files: [...string]
			}
			for _, _filename in glob.files {
				"delete \(_filename)": file.RemoveAll & {
					path: _filename
				}
			}
		}
		for _workflowName, _workflow in github.workflow {
			let _filename = _workflowName + ".yml"
			"generate \(_filename)": file.Create & {
				$after: [ for v in remove {v}]
				filename: path.Join([_dir, _filename], _goos)
				let donotedit = "Code generated by \(_this_file); DO NOT EDIT."
				contents: "# \(donotedit)\n\n\(yaml.Marshal(_workflow))"
			}
		}
	}
}
```

{{step}}
Modify line 1's package name to match the name of the directory containing the file exactly. 

{{< info >}}
CUE package identifiers can only contain alphanumerics and underscores, so the
directory directly containing the `github_actions_tool.cue` file must also
contain only these characters.
{{< /info >}}

{{step}}
Modify line 8 so that it contains:
- the name of the CUE module that you initialised in step FIXME:step-ref{cue mod init}; and
- the path from the root of the repository to the CUE workflow files' dedicated directory;
- separated by a forward slash ("`/`").

{{step}}
Modify line 16 so that it contains the appropriate number of relative "`../`"
elements to move:
- from the directory containing the `github_actions_tool.cue`;
- to the `.github/workflows` directory containing the YAML-based workflow files.

{{step}}
Modify line 17 so that it contains the correct path of the
`github_actions_tool.cue` file inside your repository.

{{step}}
With the modified `github_actions_tool.cue` file in place, change into the
directory containing this file and check that the `regenerate` command is
listed by `cue help cmd` as being available. For example, using `awk` to limit
the output of `cue help cmd`:

```
$ cd $(git rev-parse --show-toplevel)
$ cd internal/ci
$ cue help cmd | awk '/Available Commands:/,/^$/'
Available Commands:
  regenerate  Regenerate all workflow files

```

If you *don't* see the `regenerate` command listed, check the contents of the
`github_actions_tool.cue` file and review the modifications you made to it, as
well as its location in the repository. Make sure you've followed all the steps
above carefully.

{{step}}
Now that the `regenerate` command is confirmed as being available, invoke it to
regenerate all YAML-based workflow files from their CUE sources. For example:

```console
$ cd $(git rev-parse --show-toplevel)
$ cd internal/ci
$ cue cmd regenerate
```

{{step}}
Check that the only change to each YAML-based workflow file is the addition of
a header, warning the reader not to edit the .yml file directly. For example:

```diff
$ cd $(git rev-parse --show-toplevel)
$ git diff .github/workflows/
diff --git a/.github/workflows/workflow1.yml b/.github/workflows/workflow1.yml
index 28e7051..416bad0 100644
--- a/.github/workflows/workflow1.yml
+++ b/.github/workflows/workflow1.yml
@@ -1,3 +1,5 @@
+# Code generated by internal/ci/ci_tool.cue; DO NOT EDIT.
+
 jobs:
   workflow1_job1:
     strategy:
diff --git a/.github/workflows/workflow2.yml b/.github/workflows/workflow2.yml
index 813ebf6..622f998 100644
--- a/.github/workflows/workflow2.yml
+++ b/.github/workflows/workflow2.yml
@@ -1,3 +1,5 @@
+# Code generated by internal/ci/ci_tool.cue; DO NOT EDIT.
+
 jobs:
   workflow2_job1:
     runs-on: ubuntu-latest
```

{{step}}
Commit the following files and directories to git:
- `.github/workflows/*.yml`
- `internal/ci/` (or the location you chose, if different)

{{step}
Now, each time you make a change to a CUE workflow file, immediately run `cue
cmd regenerate` inside the CUE workflow file directory to regenerate the YAML
files that GitHub Actions requires, and commit your changes to both the CUE and
YAML files. For example:

```console
$ cd $(git rev-parse --show-toplevel)
$ cd internal/ci
$ cue cmd regenerate
```

{{step}} Well done! Your GitHub Actions workflow files have been imported into
CUE, and can be managed using CUE's safer, more expressive, and more powerful
features.

FIXMEs:

- debug process for `_tool` file errors is poor - should we give them any pointers?

#### Further reading/See Also

- [cmd/cue command line documentation](https://cue.googlesource.com/cue/+/refs/tags/v0.2.0/doc/cmd/cue.md)
