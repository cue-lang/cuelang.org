---
title: Migrating to the Central Registry's curated Kubernetes Schemas
---

If you're already using CUE to validate or generate Kubernetes manifests then
you probably used the `cue get go` command to create the relevant packages and
schemas:
```text { title="TERMINAL" type="terminal" codeToCopy="Y3VlIGdldCBnbyBrOHMuaW8vYXBpL2FwcHMvdjE=" }
$ cue get go k8s.io/api/apps/v1
```

This command generates and stores CUE schemas under `cue.mod/gen/k8s.io/` after
you've also fetched the relevant Go source code.

The CUE
[Central Registry](https://registry.cue.works)
provides a well-known location for well-known modules
and schemas, including those from the Kubernetes project.
You can easily modify your CUE to rely on up-to-date, curated schemas published on the
[Central Registry](https://registry.cue.works)
by using the
[`cue refactor imports`](https://cuelang.org/docs/reference/command/cue-help-refactor-imports/)
command. This guide shows you how.

The `cue refactor imports` command is not yet available in the latest CUE release.
To use the command,
[install](https://cuelang.org/docs/introduction/installation/#download-an-official-cue-binary)
the most recent CUE prerelease. This page demonstrates the following version of CUE:

```text { title="TERMINAL" type="terminal" codeToCopy="Y3VlIHZlcnNpb24=" }
$ cue version
cue version v0.13.0-alpha.2
...
```

## Check your current CUE
Having schemas generated by `cue get go` has allowed you to validate Kubernetes
manifests, or to generate them from CUE code like this:
```cue { title="manifest.cue" codeToCopy="cGFja2FnZSBrdWJlCgppbXBvcnQgYXBwcyAiazhzLmlvL2FwaS9hcHBzL3YxIgoKYXBwcy4jRGVwbG95bWVudCAmIHsKCWFwaVZlcnNpb246ICJhcHBzL3YxIgoJa2luZDogICAgICAgIkRlcGxveW1lbnQiCgltZXRhZGF0YTogewoJCWxhYmVsczogYXBwOiAiZXhhbXBsZTEiCgkJbmFtZTogImV4YW1wbGUxIgoJfQoJc3BlYzogewoJCXJlcGxpY2FzOiAxCgkJc2VsZWN0b3I6IG1hdGNoTGFiZWxzOiBhcHA6ICJleGFtcGxlMSIKCQl0ZW1wbGF0ZTogewoJCQltZXRhZGF0YTogbGFiZWxzOiBhcHA6ICJleGFtcGxlMSIKCQkJc3BlYzogY29udGFpbmVyczogW3sKCQkJCWltYWdlOiAibmdpbng6bGF0ZXN0IgoJCQkJbmFtZTogICJuZ2lueCIKCQkJfV0KCQl9Cgl9Cn0K" }
// filepath: manifest.cue

package kube

import apps "k8s.io/api/apps/v1"

apps.#Deployment & {
	apiVersion: "apps/v1"
	kind:       "Deployment"
	metadata: {
		labels: app: "example1"
		name: "example1"
	}
	spec: {
		replicas: 1
		selector: matchLabels: app: "example1"
		template: {
			metadata: labels: app: "example1"
			spec: containers: [{
				image: "nginx:latest"
				name:  "nginx"
			}]
		}
	}
}
```
The `import` line, at the top of this CUE file, specifies the location of the
schemas previously generated from the Kubernetes source code.
These schemas allow the `cue vet` command to catch validation problems in our
manifest:
```text { title="TERMINAL" type="terminal" codeToCopy="Y3VlIHZldCAtYw==" }
$ cue vet -c
```
Because `cue vet` doesn't print any errors, we know that the manifest currently
validates successfully.

## Authenticate to the Central Registry
You need to be authenticated to the
[Central Registry](https://registry.cue.works)
-- so if you haven't
done so previously you'll need to login:
```text { title="TERMINAL" type="terminal" codeToCopy="Y3VlIGxvZ2lu" }
$ cue login
```
This allows you to fetch the curated Kubernetes module, which contains the curated schemas.

## Update your CUE files
To update the relevant imports across every CUE file in your module, run the
[`cue refactor imports`](https://cuelang.org/docs/reference/command/cue-help-refactor-imports/)
command with a "before" and "after" prefix.
In the case of Kubernetes schemas, this is the command you need to run:

```text { title="TERMINAL" type="terminal" codeToCopy="Y3VlIHJlZmFjdG9yIGltcG9ydHMgazhzLmlvIHRlc3QuY3VlLndvcmtzL3gxL2s4cy5pbw==" }
$ cue refactor imports k8s.io test.cue.works/x1/k8s.io
```

CUE files are re-written, updating `import` lines as required:
```cue { title="manifest.cue" codeToCopy="cGFja2FnZSBrdWJlCgppbXBvcnQgYXBwcyAidGVzdC5jdWUud29ya3MveDEvazhzLmlvL2FwaS9hcHBzL3YxIgoKYXBwcy4jRGVwbG95bWVudCAmIHsKCWFwaVZlcnNpb246ICJhcHBzL3YxIgoJa2luZDogICAgICAgIkRlcGxveW1lbnQiCgltZXRhZGF0YTogewoJCWxhYmVsczogYXBwOiAiZXhhbXBsZTEiCgkJbmFtZTogImV4YW1wbGUxIgoJfQoJc3BlYzogewoJCXJlcGxpY2FzOiAxCgkJc2VsZWN0b3I6IG1hdGNoTGFiZWxzOiBhcHA6ICJleGFtcGxlMSIKCQl0ZW1wbGF0ZTogewoJCQltZXRhZGF0YTogbGFiZWxzOiBhcHA6ICJleGFtcGxlMSIKCQkJc3BlYzogY29udGFpbmVyczogW3sKCQkJCWltYWdlOiAibmdpbng6bGF0ZXN0IgoJCQkJbmFtZTogICJuZ2lueCIKCQkJfV0KCQl9Cgl9Cn0K" }
// filepath: manifest.cue

package kube

import apps "test.cue.works/x1/k8s.io/api/apps/v1"

apps.#Deployment & {
	apiVersion: "apps/v1"
	kind:       "Deployment"
	metadata: {
		labels: app: "example1"
		name: "example1"
	}
	spec: {
		replicas: 1
		selector: matchLabels: app: "example1"
		template: {
			metadata: labels: app: "example1"
			spec: containers: [{
				image: "nginx:latest"
				name:  "nginx"
			}]
		}
	}
}
```
The `import` line at the top now references the appropriate curated module for
the deployment contained in the manifest. Its path is currently temporary, but
only while its proper location is being decided. The temporary path isn’t a
problem because one important property of the
[Central Registry](https://registry.cue.works)
is that, once a schema is published, it will always be available at that
location. When the curated module’s location is finalised and versions are
published under the new path, you can easily use the `cue refactor imports`
command to update your CUE again!

## Tidy your CUE module
```text { title="TERMINAL" type="terminal" codeToCopy="Y3VlIG1vZCB0aWR5" }
$ cue mod tidy
```
Tidying your CUE module fetches the Kubernetes module from the
[Central Registry](https://registry.cue.works),
and updates the `cue.mod/module.cue` file to track this dependency:

```text { title="TERMINAL" type="terminal" codeToCopy="Y2F0IGN1ZS5tb2QvbW9kdWxlLmN1ZQ==" }
$ cat cue.mod/module.cue
module: "kube.example"
language: {
	version: "v0.13.0"
}
deps: {
	"test.cue.works/x1/k8s.io@v0": {
		v: "v0.3.0"
	}
}
```

Updating to a later version of a curated module is performed by a different
command (not `cue refactor imports`) -- we'll be publishing a guide about how
to do that, soon!

## Remove the old packages and schemas
The schemas stored in the `cue.mod/gen/k8s.io/` directory are no longer needed,
and should be deleted:
```text { title="TERMINAL" type="terminal" codeToCopy="cm0gLXIgY3VlLm1vZC9nZW4vazhzLmlvLw==" }
$ rm -r cue.mod/gen/k8s.io/
```

## Check your updated CUE
```text { title="TERMINAL" type="terminal" codeToCopy="Y3VlIHZldCAtYw==" }
$ cue vet -c
```
Because `cue vet` doesn't print any errors, we know that the manifest still
validates successfully against the new schemas.
