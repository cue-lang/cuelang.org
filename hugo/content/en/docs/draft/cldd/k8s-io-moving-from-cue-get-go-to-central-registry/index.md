---
title: Migrating to the Central Registry's curated Kubernetes Schemas
summary: >
 This guide shows you how to migrate your CUE from using Kubernetes schemas
 generated by "cue get go" to the newer, curated k8s.io module published on
 the CUE Central Registry.
---

If you're already using CUE to validate or generate Kubernetes manifests then
you probably used the `cue get go` command to create the relevant packages and
schemas:
``` { .text title="TERMINAL" data-copy="cue get go k8s.io/api/apps/v1" }
$ cue get go k8s.io/api/apps/v1
```

This command generates and stores CUE schemas under `cue.mod/gen/k8s.io/` after
you've also fetched the relevant Go source code.

The CUE
[Central Registry](https://registry.cue.works)
provides a well-known location for well-known modules
and schemas, including those from the Kubernetes project.
You can easily modify your CUE to rely on up-to-date, curated schemas published on the
[Central Registry](https://registry.cue.works)
by using the
[`cue refactor imports`](https://cuelang.org/docs/reference/command/cue-help-refactor-imports/)
command. This guide shows you how.

## Check your current CUE
Having schemas generated by `cue get go` has allowed you to validate Kubernetes
manifests, or to generate them from CUE code like this:
``` { .cue title="manifest.cue" }
package kube

import apps "k8s.io/api/apps/v1"

apps.#Deployment & {
	apiVersion: "apps/v1"
	kind:       "Deployment"
	metadata: {
		labels: app: "example1"
		name: "example1"
	}
	spec: {
		replicas: 1
		selector: matchLabels: app: "example1"
		template: {
			metadata: labels: app: "example1"
			spec: containers: [{
				image: "nginx:latest"
				name:  "nginx"
			}]
		}
	}
}
```
The `import` line, at the top of this CUE file, specifies the location of the
schemas previously generated from the Kubernetes source code.
These schemas allow the `cue vet` command to catch validation problems in our
manifest:
``` { .text title="TERMINAL" data-copy="cue vet -c" }
$ cue vet -c
```
Because `cue vet` doesn't print any errors, we know that the manifest currently
validates successfully.

## Update your CUE files
To update the relevant imports across every CUE file in your module, run the
[`cue refactor imports`](https://cuelang.org/docs/reference/command/cue-help-refactor-imports/)
command with a "before" and "after" prefix.
In the case of Kubernetes schemas, this is the command you need to run:

``` { .text title="TERMINAL" data-copy="cue refactor imports k8s.io cue.dev/x/k8s.io" }
$ cue refactor imports k8s.io cue.dev/x/k8s.io
```

CUE files are re-written, updating `import` lines as required:

``` { .cue title="manifest.cue" }
package kube

import apps "cue.dev/x/k8s.io/api/apps/v1"

apps.#Deployment & {
	apiVersion: "apps/v1"
	kind:       "Deployment"
	metadata: {
		labels: app: "example1"
		name: "example1"
	}
	spec: {
		replicas: 1
		selector: matchLabels: app: "example1"
		template: {
			metadata: labels: app: "example1"
			spec: containers: [{
				image: "nginx:latest"
				name:  "nginx"
			}]
		}
	}
}
```

The `import` line at the top now references the appropriate curated module for
the deployment contained in the manifest. Its path is currently temporary, but
only while its proper location is being decided. The temporary path isn’t a
problem because one important property of the
[Central Registry](https://registry.cue.works)
is that, once a schema is published, it will always be available at that
location. When the curated module’s location is finalised and versions are
published under the new path, you can easily use the `cue refactor imports`
command to update your CUE again!

## Tidy your CUE module
``` { .text title="TERMINAL" data-copy="cue mod tidy" }
$ cue mod tidy
```

If you see an error message mentioning "too many requests" then
[login to the Central Registry](../login-central-registry.md)
and re-run this command.
The Central Registry allows more requests from authenticated users.

Tidying your CUE module fetches the Kubernetes module from the
[Central Registry](https://registry.cue.works),
and updates the `cue.mod/module.cue` file to track this dependency:

``` { .text title="TERMINAL" data-copy="cat cue.mod/module.cue" }
$ cat cue.mod/module.cue
module: "kube.example"
language: {
	version: "v0.14.0"
}
deps: {
	"cue.dev/x/k8s.io@v0": {
		v: "v0.0.0"
	}
}
```

Updating to a later version of a curated module is performed by a different
command (not `cue refactor imports`) -- we'll be publishing a guide about how
to do that, soon!

## Remove the old packages and schemas
The schemas stored in the `cue.mod/gen/k8s.io/` directory are no longer needed,
and should be deleted:
``` { .text title="TERMINAL" data-copy="rm -r cue.mod/gen/k8s.io/" }
$ rm -r cue.mod/gen/k8s.io/
```

## Check your updated CUE
``` { .text title="TERMINAL" data-copy="cue vet -c" }
$ cue vet -c
```
Because `cue vet` doesn't print any errors, we know that the manifest still
validates successfully against the new schemas.
